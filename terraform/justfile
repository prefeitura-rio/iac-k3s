bucket := "bucket=iplanrio-dia-terraform"
tfplan := "terraform.tfplan"
jump_host := env("JUMP_HOST")
target_host := env("TARGET_HOST")

# Authenticate with Google Cloud and Infisical
@auth:
    gcloud auth application-default login
    infisical login

# Run the Ansible playbook
@deploy:
    ansible-playbook -i inventory.ini playbook.yaml

# Test connection to all hosts
@ping:
    ansible all -i inventory.ini -m ping

# Forward Incus API and k8s API ports from remote to local
@forward:
    ssh -f -N -L 8443:localhost:8443 -J {{jump_host}} k3s@{{target_host}}
    ssh -f -N -L 6443:$(terraform output -raw k3s_master_ip):6443 -J {{jump_host}} k3s@{{target_host}}

# Stop all port forwarding processes
@stop-forward:
    -pkill -f "ssh.*{{target_host}}"

# Init Terraform with backend config
@init args="":
    terraform init -backend-config {{bucket}} -upgrade {{args}} -reconfigure

# Plan Terraform changes
@plan:
    infisical run -- terraform plan -out {{tfplan}}

# Destroy Terraform-managed infrastructure
@destroy:
    infisical run -- terraform destroy -auto-approve
# Apply Terraform changes
@apply:
    infisical run -- terraform apply {{tfplan}}
