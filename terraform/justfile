bucket := "bucket=iplanrio-dia-terraform"
tfplan := "terraform.tfplan"

# Authenticate with Google Cloud and Infisical
@auth:
    gcloud auth application-default login
    infisical login

# Run the Ansible playbook
@deploy:
    ansible-playbook -i inventory.ini playbook.yaml

# Test connection to all hosts
@ping:
    ansible all -i inventory.ini -m ping

# Forward Incus API port from remote to local
@forward-incus:
    infisical run -- sh -c 'ssh -f -N -L 8443:localhost:8443 -J "$TF_VAR_jump_host" "k3s@$TF_VAR_target_host"'

# Forward Kubernetes API port from remote to local
@forward-k8s:
    infisical run -- sh -c 'ssh -f -N -L 6443:$(terraform output -raw k3s_master_ip):6443 -J "$TF_VAR_jump_host" "k3s@$TF_VAR_target_host"'

# Forward both Incus and Kubernetes API ports
@forward: forward-incus forward-k8s

# Stop all port forwarding processes
@stop-forward:
    infisical run -- sh -c 'pkill -f "ssh.*$TF_VAR_target_host"'

# Init Terraform with backend config
@init args="":
    terraform init -backend-config {{bucket}} -upgrade {{args}} -reconfigure

# Plan Terraform changes
@plan:
    infisical run -- terraform plan -out {{tfplan}}

# Destroy Terraform-managed infrastructure
@destroy:
    infisical run -- terraform destroy -auto-approve
# Apply Terraform changes
@apply:
    infisical run -- terraform apply {{tfplan}}
